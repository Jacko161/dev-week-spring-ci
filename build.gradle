plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.12.0'
}

group = 'com.instaclustr.jfleming'
version = '0.0.1'
sourceCompatibility = '16'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

test {
	finalizedBy jacocoTestCoverageVerification, jacocoTestReport // report is always generated after tests run
	jacoco {
	}
}


jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	reports {
		xml.required=true // coveralls plugin depends on xml format report
		html.required=true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			excludes = ["com.instaclustr.jfleming.devweek.DevweekApplication"]
			limit {
				minimum = 0.95
			}
		}
	}
}

coveralls {
	jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

repositories {
	mavenCentral()
	maven {
		url "https://devweek.jfrog.io/artifactory/maven-local-libs-release"
	}
}

task printVersion {
	// any code that goes here is part of configuring the task
	// this code will always get run, even if the task is not executed
	doLast { // add a task action
		// any code that goes here is part of executing the task
		// this code will only get run if and when the task gets executed
		println project.version
	}
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'com.instaclustr:library:0.0.4'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testImplementation 'com.h2database:h2:1.4.194'
}

test {
	useJUnitPlatform()
}
